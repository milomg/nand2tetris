// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    Mux16(a=aluout, b=instruction, sel=seta, out=ain);   // Decide whether to pass ALU output or the instruction contents to A

    Not(in=instruction[15],out=seta);                    // We write the instruction to A if the first bit says we are in an A-instruction
    Or(a=seta, b=instruction[5], out=reala);             // We also write the instruction to A if we are in a C-instruction and the fifth bit is 1
    ARegister(in=ain, load=reala, out=rega, out[0..14]=addressM); // Pass the output of the A register to the address and to a temporary wire (that may get passed into the ALU)

    Mux16(a=rega, b=inM, sel=instruction[12], out=alux); // We pass either the A register or memory based on the "a" bit in ixxaccccccdddjjj

    And(a=instruction[4], b=instruction[15], out=writed);// We only write to D if we are in a C-instruction and the fifth bit (D destination bit) is 1
    DRegister(in=aluout, load=writed, out=regd);

    // Pass our flags to the ALU (as well as the A,M,D registers)
    ALU(x=regd, y=alux, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=aluout, out=outM, zr=aluzr, ng=alung);
    
    And(a=alung, b=instruction[2], out=shouldjump1);  // Do we want to jump when negative and the ALU result is negative?
    And(a=aluzr, b=instruction[1], out=shouldjump2);  // Do we want to jump when zero and the ALU result is zero?
    
    Or(a=alung,b=aluzr,out=alungzr);
    Not(in=alungzr,out=alups);
    And(a=alups, b=instruction[0], out=shouldjump3);  // Do we want to jump when positive and the ALU result is positive, i.e. not (negative OR zero)?

    Or(a=shouldjump1,b=shouldjump2,out=o4);
    Or(a=shouldjump3,b=o4,out=probablyjump);          // Three way or to decide whether to jump

    And(a=instruction[15], b=probablyjump, out=jump); // Don't jump if we are in an A-instruction
    PC(in=rega, load=jump, inc=true, reset=reset, out[0..14]=pc);

    And(a=instruction[3], b=instruction[15], out=writeM); // Write to M if we are in a C-instruction and the fourth bit (M destination bit) is 1
}