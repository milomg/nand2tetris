class ArrayFunction {
    /** Returns the sum of all elements in the Array. */
    function int sum(Array a, int length) {
        var int i, sum;
        let i = 0;
        let sum = 0;
        while (i < length) {
            let sum = sum + a[i];
            let i = i + 1;
        }
        return sum;
    }

    /** Returns the average of all elements in the Array. */
    function int average(Array a, int length) {
        return ArrayFunction.sum(a, length) / length;
    }

    /** Returns a new Array that is an exact copy of the given Array */
    function Array copy(Array a, int i) {
        var Array b;
        let b = Array.new(i);
        let i = i - 1;
        while ((i > 0) | (i = 0)) {
            let b[i] = a[i];
            let i = i - 1;
        }
        return b;
    }

    /** Returns true/false for whether the given Arrays have identical contents */
    function boolean equals(Array a, Array b, int length1, int length2) {
        var int i;

        let i = 0;
        if (~(length1 = length2)) {
            return false;
        }
        while (i < length1) {
            if (~(a[i] = b[i])) {
                return false;
            }
            let i = i + 1;
        }
        return true;
    }

    /** Prints all elements in the array in the format [x,y,z] */
    function void prettyPrint(Array a, int length) {
        var int i;

        do Output.printString("[");
        while (i < length) {
            do Output.printInt(a[i]);
            if (i < (length - 1)) {
               do Output.printString(",");
            }
            let i = i + 1;
        }
        do Output.printString("]");
        return;
    }

}
